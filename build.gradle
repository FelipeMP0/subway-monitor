plugins {
    id "com.diffplug.spotless" version "6.4.2"
    id "com.avast.gradle.docker-compose" version "0.15.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

ext.commonLibs = [
        jacksonVersion: "2.13.2",
        opentableVersion: "0.13.3",
        flywayVersion: "8.5.8",
]

allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.spotless"

    group "com.subwaymonitor"
    version "1.0"

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.6.6")
        }
    }

    dependencies {
        testImplementation group: "org.springframework.boot", name: "spring-boot-starter-test", {
            exclude group: "junit", module: "junit"
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }
        testImplementation group: "org.junit.jupiter", name: "junit-jupiter", version: "5.8.2"
    }

    tasks.named("test") {
        useJUnitPlatform()
    }

    spotless {
        java {
            target "**/java/**/*.java"
            importOrder()
            googleJavaFormat()
            removeUnusedImports()

            custom 'noWildcardImports', {
                if (it.contains('*;\n')) {
                    throw new Error('No wildcard imports allowed')
                }
            }
        }
    }

    compileJava.dependsOn 'spotlessApply'
}
